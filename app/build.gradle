plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.stripe.aod.sampleapp'
    compileSdk 33

    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)
    def backEndUrl = properties.getProperty('BACKEND_URL')

    defaultConfig {
        applicationId "com.stripe.aod.sampleapp"
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "BACKEND_URL", "\"${backEndUrl.replaceAll("^[\"\\s]*|[\"\\s]*\$", "")}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.android.material
    implementation libs.androidx.core.ktx
    implementation libs.androidx.swiperefreshlayout

    // ViewModel and LiveData
    implementation libs.androidx.lifecycle.livedata
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.viewmodel

    implementation libs.kotlinx.coroutines.android

    implementation libs.androidx.fragment.ktx

    // OK HTTP
    implementation libs.okhttp

    // Retrofit
    implementation libs.retrofit2.retrofit
    implementation libs.retrofit2.converter.gson

    // Stripe Terminal library
    implementation libs.stripe.terminal.core
    implementation libs.stripe.terminal.handoffclient

    // navigation
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
}